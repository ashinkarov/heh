; This example demonstrates how to define `zip' in Heh.
; `zip' takes two vectors and produce a vector of pairs.

; Helper functions
letrec vecminus = \x.\y. imap |x| {_(iv): x.iv - y.iv in
letrec vecplus = \x.\y. imap |x| {_(iv): x.iv + y.iv in
letrec min = \x.\y. imap |x| {_(iv): if x.iv < y.iv then x.iv else y.iv in

letrec zip = \a.\b. imap (min |a| (|b|))|[2]
                         { _(iv): [a.iv, b.iv] in

; The previous version of the zip would force evaluation of the entire pair
; when any of the elements is selected.  In this version of the zip, this is
; not the case and the tuples of the final zip are lazy as well.
letrec zip1 = \a.\b. letrec shp = min |a| (|b|) in
                     imap [shp.[0], 2] { [0,0] <= iv < [shp.[0], 1]: a.[iv.[0]],
                                         [0,1] <= iv < [shp.[0], 2]: b.[iv.[0]] in

; Application examples
if (zip [1,2] [3,4,5,6]).[1,0] <> 2 then
    [false]

else if (zip (imap [omega+1] {_(iv): iv.[0]) [1,2,3]).[0,1] <> 1 then
    [false, false]

else if (zip [1,2,3] (imap [omega+1] {_(iv): iv.[0])).[0,1] <> 0 then 
    [false, false, false]

else if (zip (imap [omega+4] {_(iv): iv.[0])
             (imap [omega+55] {_(iv): iv.[0])).[omega,1]
        <> omega then
    [false, false, false, false]

else
    [true]

